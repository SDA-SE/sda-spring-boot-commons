plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  awaitalityVersion = '4.2.0'
  logbackContribVersion = '0.1.5'
  springBootVersion = '3.1.3'
  springCloudVersion = '2022.0.4'
  scalaVersion = '2.13.12'
  victoolsVersion = '4.31.1'
}

dependencies {
  api enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"), {
    exclude group: 'net.minidev', module: 'json-smart'
    exclude group: 'org.yaml', module: 'snakeyaml' // 1.x CVE-2022-1471
  }
  api enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"), {
    exclude group: 'org.yaml', module: 'snakeyaml' // 1.x CVE-2022-1471
  }

  // asyncapi generation
  api enforcedPlatform("com.github.victools:jsonschema-generator-bom:${victoolsVersion}")

  constraints {
    // overall testing dependencies
    api 'org.awaitility:awaitility:4.2.0'

    // spring cloud
    api 'org.apache.httpcomponents:httpclient:4.5.13', {
      because 'conflict spring cloud (4.5.4 via maven resolver) and spring-cloud-aws-s3 (4.5.13 via awssdk)'
    }

    // sda-commons-starter-web
    api 'org.bouncycastle:bcpkix-jdk18on:1.74', {
      because 'fix CVE-2023-33201'
    }
    api 'org.bouncycastle:bcprov-jdk18on:1.76', {
      because 'fix CVE-2023-33201'
    }
    api "ch.qos.logback.contrib:logback-json-classic:${logbackContribVersion}"
    api "ch.qos.logback.contrib:logback-jackson:${logbackContribVersion}"
    api 'io.github.springwolf:springwolf-core:0.14.0'
    api 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.1.0'

    api 'com.squareup.okio:okio:3.5.0', {
      because "< 3.4.0 CVE-2023-3635"
    }

    // should align with transitive dependency of org.springdoc:springdoc-openapi-webmvc-core:
    api 'io.swagger.core.v3:swagger-core-jakarta:2.2.9'
    api 'io.swagger.core.v3:swagger-models-jakarta:2.2.9'
    api 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.9'

    api 'io.github.classgraph:classgraph:4.8.162'
    api 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    api 'org.yaml:snakeyaml:2.0', {
      because 'Temporary: this version is used to solve CVE-2022-1471'
    }
    api 'org.xerial.snappy:snappy-java:1.1.10.4', {
      because 'fix CVE-2023-34455, CVE-2023-43642'
    }

    // sda-commons-web-testing
    api 'com.nimbusds:nimbus-jose-jwt:9.35'
    api 'com.github.tomakehurst:wiremock-jre8-standalone:2.35.0'
    api 'net.minidev:json-smart:2.5.0', {
      because 'Temporary: this version is used to solve CVE-2023-1370'
    }
    api 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring30x:4.7.0'

    // sda-commons-starter-kafka
    api "org.scala-lang:scala-reflect:${scalaVersion}", {
      because 'conflict between org.apache.kafka:kafka_2.13:3.1.1 and com.typesafe.scala-logging:scala-logging_2.13:3.9.3'
    }
    api "org.scala-lang:scala-library:${scalaVersion}", {
      because 'conflict between com.fasterxml.jackson.module:jackson-module-scala_2.13:2.13.3, com.typesafe.scala-logging:scala-logging_2.13:3.9.3 and org.scala-lang.modules:scala-collection-compat_2.13:2.4.4'
    }
    api "org.apache.zookeeper:zookeeper:3.6.4", {
      because 'version conflict in transitive managed depedencies'
    }

    // sda-commons-starter-s3
    api "io.awspring.cloud:spring-cloud-aws-s3:3.0.1"

  }
}
