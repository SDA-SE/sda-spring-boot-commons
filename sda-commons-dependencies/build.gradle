plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  bouncycastleVersion = '1.81'
  logbackContribVersion = '0.1.5'
  springBootVersion = '3.5.3'
  springCloudVersion = '2025.0.0'
  mongoDbDriverVersion = '5.4.0' // last version supporting AWS DocumentDB 4
  scalaVersion = '2.13.16'
  swaggerCoreVersion = '2.2.34'
  victoolsVersion = '4.38.0'
}

dependencies {
  // Spring Boot after MongoDB so that MongoDB overrules
  api enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"), {
    exclude group: 'org.mongodb'
  }
  api enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"), {
    exclude group: 'org.mongodb'
  }
  // MongoDB 5.5.x is not compatible with MongoDB 4.0 and AWS DocumentDB 4
  api enforcedPlatform("org.mongodb:mongodb-driver-bom:${mongoDbDriverVersion}")

  // asyncapi generation
  api enforcedPlatform("com.github.victools:jsonschema-generator-bom:${victoolsVersion}")

  constraints {
    // overall conflicts
    api 'org.slf4j:slf4j-api:2.0.17'
    api 'com.google.errorprone:error_prone_annotations:2.39.0', {
      because 'conflicts between gson via jose-jwt and spring and caffeine via kafka-storage and spring'
    }

    // overall testing dependencies
    api 'org.awaitility:awaitility:4.3.0'

    api 'org.junit-pioneer:junit-pioneer:2.3.0'

    api 'org.junit:junit-bom:5.13.3', {
      because 'conflict junit-pioneer and spring-boot-starter-test'
    }

    // spring cloud
    api 'org.apache.httpcomponents:httpclient:4.5.14', {
      because 'conflict spring cloud (4.5.4 via maven resolver) and spring-cloud-aws-s3 (4.5.13 via awssdk)'
    }
    api 'commons-io:commons-io:2.19.0', {
      because 'conflict robothy, spring cloud, zookeeper'
    }

    // sda-commons-starter-web
    api "org.bouncycastle:bcpkix-jdk18on:${bouncycastleVersion}", {
      because 'fix CVE-2023-33201'
    }
    api "org.bouncycastle:bcprov-jdk18on:${bouncycastleVersion}", {
      because 'fix CVE-2023-33201'
    }
    api "ch.qos.logback.contrib:logback-json-classic:${logbackContribVersion}"
    api "ch.qos.logback.contrib:logback-jackson:${logbackContribVersion}"
    api 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.8.9'
    api "io.opentelemetry:opentelemetry-api-incubator:1.51.0-alpha", {
      because "conflict within transitive dependencies of Spring Boot Dependencies"
    }

    // should align with transitive dependency of org.springdoc:springdoc-openapi-webmvc-core:
    api "io.swagger.core.v3:swagger-core-jakarta:${swaggerCoreVersion}"
    api "io.swagger.core.v3:swagger-models-jakarta:${swaggerCoreVersion}"
    api "io.swagger.core.v3:swagger-annotations-jakarta:${swaggerCoreVersion}"

    api 'io.github.classgraph:classgraph:4.8.180'
    api 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    api 'org.xerial.snappy:snappy-java:1.1.10.7', {
      because 'fix CVE-2023-34455, CVE-2023-43642'
    }

    // sda-commons-web-testing
    api 'com.nimbusds:nimbus-jose-jwt:10.3.1'

    api 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring3x:4.20.0'

    // sda-commons-starter-mongodb
    api "org.mongodb:mongodb-driver-sync:${mongoDbDriverVersion}"
    api "org.mongodb:bson:${mongoDbDriverVersion}"
    api "org.mongodb:mongodb-driver-core:${mongoDbDriverVersion}"
    api "org.mongodb:bson-record-codec:${mongoDbDriverVersion}"

    // sda-commons-starter-kafka
    api "org.scala-lang:scala-reflect:${scalaVersion}", {
      because 'conflict between org.apache.kafka:kafka_2.13:3.1.1 and com.typesafe.scala-logging:scala-logging_2.13:3.9.3'
    }
    api "org.scala-lang:scala-library:${scalaVersion}", {
      because 'conflict between com.fasterxml.jackson.module:jackson-module-scala_2.13:2.13.3, com.typesafe.scala-logging:scala-logging_2.13:3.9.3 and org.scala-lang.modules:scala-collection-compat_2.13:2.4.4'
    }
    api "org.apache.zookeeper:zookeeper:3.9.3", {
      because 'version conflict in transitive managed dependencies'
    }

    // sda-commons-starter-s3
    api "io.awspring.cloud:spring-cloud-aws-s3:3.4.0"
    api "io.github.robothy:local-s3-rest:1.24"

  }
}
