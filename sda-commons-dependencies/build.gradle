plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  bouncycastleVersion = '1.83' //check to be aligned with spring cloud
  logbackContribVersion = '0.1.5'
  springBootVersion = '4.0.3'
  springCloudVersion = '2025.1.1'
  mongoDbDriverVersion = '5.6.4'
  scalaVersion = '2.13.18'
  swaggerCoreVersion = '2.2.43'
  victoolsVersion = '5.0.0'
}

dependencies {

  api enforcedPlatform("tools.jackson:jackson-bom:3.1.0")

  // Spring Boot after MongoDB so that MongoDB overrules
  api enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"), {
    exclude group: 'org.mongodb'
  }
  api enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"), {
    exclude group: 'org.mongodb'
  }
  api enforcedPlatform("org.mongodb:mongodb-driver-bom:${mongoDbDriverVersion}")

  // asyncapi generation
  api enforcedPlatform("com.github.victools:jsonschema-generator-bom:${victoolsVersion}")

  api enforcedPlatform("org.eclipse.jetty:jetty-bom:12.1.6")
  api enforcedPlatform("org.eclipse.jetty.ee10:jetty-ee10-bom:12.1.6")

  constraints {
    // overall conflicts
    api 'org.slf4j:slf4j-api:2.0.17'
    api 'com.google.errorprone:error_prone_annotations:2.47.0', {
      because 'conflicts between gson via jose-jwt and spring and caffeine via kafka-storage and spring'
    }

    // overall testing dependencies
    api 'org.awaitility:awaitility:4.3.0'
    api 'org.junit-pioneer:junit-pioneer:2.3.0'

    // spring cloud
    api 'org.apache.httpcomponents:httpclient:4.5.14', {
      because 'conflict spring cloud (4.5.4 via maven resolver) and spring-cloud-aws-s3 (4.5.13 via awssdk)'
    }

    api 'commons-io:commons-io:2.21.0', {
      because 'conflict de.flapdoodle.embed.mongo.spring4x'
    }

    // sda-commons-starter-web
    api "org.bouncycastle:bcpkix-jdk18on:${bouncycastleVersion}"
    api "org.bouncycastle:bcprov-jdk18on:${bouncycastleVersion}"
    api "ch.qos.logback.contrib:logback-json-classic:${logbackContribVersion}"
    api "ch.qos.logback.contrib:logback-jackson:${logbackContribVersion}"
    api 'org.springdoc:springdoc-openapi-starter-webmvc-api:3.0.2'

    // should align with transitive dependency of org.springdoc:springdoc-openapi-webmvc-core:
    api "io.swagger.core.v3:swagger-core-jakarta:${swaggerCoreVersion}"
    api "io.swagger.core.v3:swagger-models-jakarta:${swaggerCoreVersion}"
    api "io.swagger.core.v3:swagger-annotations-jakarta:${swaggerCoreVersion}"

    api 'io.github.classgraph:classgraph:4.8.184'
    api 'javax.ws.rs:javax.ws.rs-api:2.1.1'

    // sda-commons-web-testing
    api 'com.nimbusds:nimbus-jose-jwt:10.8'

    api 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring4x:4.24.0'

    // sda-commons-starter-mongodb
    api "org.mongodb:mongodb-driver-sync:${mongoDbDriverVersion}"
    api "org.mongodb:bson:${mongoDbDriverVersion}"
    api "org.mongodb:mongodb-driver-core:${mongoDbDriverVersion}"
    api "org.mongodb:bson-record-codec:${mongoDbDriverVersion}"

    // sda-commons-starter-kafka
    api "org.scala-lang:scala-reflect:${scalaVersion}"
    api "org.scala-lang:scala-library:${scalaVersion}"

    // sda-commons-starter-s3
    api "io.awspring.cloud:spring-cloud-aws-s3:4.0.0"
    api "io.github.robothy:local-s3-rest:2.2"

    api "org.ow2.asm:asm:9.9.1", {
      because "conflict between starter-test (minidev) and spring-cloud (eclipse sisu)"
    }

    api "org.wiremock.integrations:wiremock-spring-boot-standalone:4.1.0"

    //Check on org.springframework.cloud:spring-cloud-openfeign-core update if still needed
    api "commons-fileupload:commons-fileupload:1.6.0", {
      because "CVE-2025-48976"
    }
  }
}
