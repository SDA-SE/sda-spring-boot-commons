plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  bouncycastleVersion = '1.82'
  logbackContribVersion = '0.1.5'
  // Check on upgrade, if org.apache.commons:commons-lang3 enforcement below (L25 + L58) is still needed!
  springBootVersion = '3.5.6'
  // Check on upgrade, if commons-fileupload:commons-fileupload enforcement below (L67) is still needed!
  springCloudVersion = '2025.0.0'
  mongoDbDriverVersion = '5.6.0'
  scalaVersion = '2.13.16'
  swaggerCoreVersion = '2.2.38'
  victoolsVersion = '4.38.0'
  nettyVersion = '4.1.127.Final' // due to CVE-2025-58057
}

dependencies {
  // Spring Boot after MongoDB so that MongoDB overrules
  api enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"), {
    exclude group: 'org.mongodb'
    exclude group: 'org.apache.commons', module: 'commons-lang3'
  }
  api enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"), {
    exclude group: 'org.mongodb'
  }
  api enforcedPlatform("org.mongodb:mongodb-driver-bom:${mongoDbDriverVersion}")

  // due to CVE-2025-58057: override netty version (spring-boot-dependencies 3.5.5 has the vulnerable version)
  // Check on upgrade of spring-boot-dependencies, if this is still needed!
  api enforcedPlatform("io.netty:netty-bom:${nettyVersion}")

  // asyncapi generation
  api enforcedPlatform("com.github.victools:jsonschema-generator-bom:${victoolsVersion}")

  constraints {
    // overall conflicts
    api 'org.slf4j:slf4j-api:2.0.17'
    api 'com.google.errorprone:error_prone_annotations:2.42.0', {
      because 'conflicts between gson via jose-jwt and spring and caffeine via kafka-storage and spring'
    }

    // overall testing dependencies
    api 'org.awaitility:awaitility:4.3.0'

    api 'org.junit-pioneer:junit-pioneer:2.3.0'

    api 'org.junit:junit-bom:5.13.4', {
      because 'conflict junit-pioneer and spring-boot-starter-test'
    }

    // spring cloud
    api 'org.apache.httpcomponents:httpclient:4.5.14', {
      because 'conflict spring cloud (4.5.4 via maven resolver) and spring-cloud-aws-s3 (4.5.13 via awssdk)'
    }
    api 'commons-io:commons-io:2.20.0', {
      because 'conflict robothy, spring cloud, zookeeper'
    }
    api 'org.apache.commons:commons-lang3:3.19.0', {
      because '''\
          Upgrade managed dependency of org.springframework.boot:spring-boot-dependencies:3.5.3
          to fix CVE-2025-48924 in 3.17.0.
          Check for removal on upgrade of io.javaoperatorsdk:operator-framework > 3.5.3
          '''
    }
    api 'commons-fileupload:commons-fileupload:1.6.0', {
      because '''\
          Upgrade transitive dependency of io.github.openfeign:feign-form-spring:13.6
          to fix CVE-2025-48976 in 1.5.
          Check for removal on upgrade of io.github.openfeign:feign-form-spring > 13.6 via
          org.springframework.cloud:spring-cloud-dependencies.
          '''
    }

    // sda-commons-starter-web
    api "org.bouncycastle:bcpkix-jdk18on:${bouncycastleVersion}", {
      because 'fix CVE-2023-33201'
    }
    api "org.bouncycastle:bcprov-jdk18on:${bouncycastleVersion}", {
      because 'fix CVE-2023-33201'
    }
    api "ch.qos.logback.contrib:logback-json-classic:${logbackContribVersion}"
    api "ch.qos.logback.contrib:logback-jackson:${logbackContribVersion}"
    api 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.8.13'
    api "io.opentelemetry:opentelemetry-api-incubator:1.54.1-alpha", {
      because "conflict within transitive dependencies of Spring Boot Dependencies"
    }

    // should align with transitive dependency of org.springdoc:springdoc-openapi-webmvc-core:
    api "io.swagger.core.v3:swagger-core-jakarta:${swaggerCoreVersion}"
    api "io.swagger.core.v3:swagger-models-jakarta:${swaggerCoreVersion}"
    api "io.swagger.core.v3:swagger-annotations-jakarta:${swaggerCoreVersion}"

    api 'io.github.classgraph:classgraph:4.8.184'
    api 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    api 'org.xerial.snappy:snappy-java:1.1.10.8', {
      because 'fix CVE-2023-34455, CVE-2023-43642'
    }

    // sda-commons-web-testing
    api 'com.nimbusds:nimbus-jose-jwt:10.5'

    api 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring3x:4.21.0'

    // sda-commons-starter-mongodb
    api "org.mongodb:mongodb-driver-sync:${mongoDbDriverVersion}"
    api "org.mongodb:bson:${mongoDbDriverVersion}"
    api "org.mongodb:mongodb-driver-core:${mongoDbDriverVersion}"
    api "org.mongodb:bson-record-codec:${mongoDbDriverVersion}"

    // sda-commons-starter-kafka
    api "org.scala-lang:scala-reflect:${scalaVersion}", {
      because 'conflict between org.apache.kafka:kafka_2.13:3.1.1 and com.typesafe.scala-logging:scala-logging_2.13:3.9.3'
    }
    api "org.scala-lang:scala-library:${scalaVersion}", {
      because 'conflict between com.fasterxml.jackson.module:jackson-module-scala_2.13:2.13.3, com.typesafe.scala-logging:scala-logging_2.13:3.9.3 and org.scala-lang.modules:scala-collection-compat_2.13:2.4.4'
    }
    api "org.apache.zookeeper:zookeeper:3.9.4", {
      because 'version conflict in transitive managed dependencies'
    }

    // sda-commons-starter-s3
    api "io.awspring.cloud:spring-cloud-aws-s3:3.4.0"
    api "io.github.robothy:local-s3-rest:2.0"

  }
}
