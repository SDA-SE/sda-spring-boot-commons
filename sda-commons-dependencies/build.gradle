plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  awaitalityVersion = '4.2.0'
  logbackContribVersion = '0.1.5'
  springBootVersion = '2.7.10'
  springCloudVersion = '2021.0.6'
  scalaVersion = '2.13.10'
}

dependencies {
  api enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"), {
    exclude group: 'net.minidev', module: 'json-smart'
  }
  api enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"), {
    exclude group: 'com.github.tomakehurst', module: 'wiremock-jre8'
  }

  constraints {
    api 'org.apache.commons:commons-lang3:3.12.0'

    // forcing upgrade due to CVE-2023-24998
    api 'commons-fileupload:commons-fileupload:1.5'

    // overall testing dependencies
    api 'org.awaitility:awaitility:4.2.0'

    // sda-commons-web-autoconfigure
    api 'org.bouncycastle:bcpkix-jdk15on:1.70', {
      because 'This version is used in org.springframework.security:spring-security-rsa:1.0.10.RELEASE'
    }
    api "ch.qos.logback.contrib:logback-json-classic:${logbackContribVersion}"
    api "ch.qos.logback.contrib:logback-jackson:${logbackContribVersion}"
    api 'org.springdoc:springdoc-openapi-webmvc-core:1.6.15'
    api 'io.github.classgraph:classgraph:4.8.157'
    api 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    api 'io.micrometer:micrometer-registry-prometheus:1.10.6'
    api 'org.yaml:snakeyaml:1.33', {
      because 'Temporary: this version is used to solve CVE-2022-25857'
    }

    // sda-commons-web-testing
    api 'com.nimbusds:nimbus-jose-jwt:9.31'
    api 'com.github.tomakehurst:wiremock-jre8:2.35.0'
    api 'net.minidev:json-smart:2.4.10', {
      because 'Temporary: this version is used to solve CVE-2023-1370'
    }

    // sda-commons-starter-kafka
    api "org.scala-lang:scala-reflect:${scalaVersion}", {
      because 'conflict between org.apache.kafka:kafka_2.13:3.1.1 and com.typesafe.scala-logging:scala-logging_2.13:3.9.3'
    }
    api "org.scala-lang:scala-library:${scalaVersion}", {
      because 'conflict between com.fasterxml.jackson.module:jackson-module-scala_2.13:2.13.3, com.typesafe.scala-logging:scala-logging_2.13:3.9.3 and org.scala-lang.modules:scala-collection-compat_2.13:2.4.4'
    }

    // sda-commons-starter-s3
    api "io.awspring.cloud:spring-cloud-aws-core:2.4.4"
  }
}
