plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  awaitalityVersion = '4.2.0'
  bouncycastleVersion = '1.77'
  logbackContribVersion = '0.1.5'
  springBootVersion = '3.2.4'
  springCloudVersion = '2023.0.0'
  scalaVersion = '2.13.13'
  swaggerCoreVersion = '2.2.21'
  victoolsVersion = '4.34.0'
}

dependencies {
  /* TODO: Next time this is updated, verify if the manual management of
     org.springframework:spring-framework-bom:6.1.5 and org.springframework.security:spring-security-core:6.2.3
     is still necessary.
     https://docs.spring.io/spring-boot/docs/current/reference/html/dependency-versions.html
   */
  api enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"), {
    exclude group: 'net.minidev', module: 'json-smart'
    exclude group: 'org.yaml', module: 'snakeyaml' // 1.x CVE-2022-1471
    exclude group: 'io.netty'
  }
  api enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"), {
    exclude group: 'org.yaml', module: 'snakeyaml' // 1.x CVE-2022-1471
    exclude group: 'com.github.tomakehurst', module: 'wiremock-jre8-standalone' // 2.35.0 / CVE-2023-41329
  }

  api enforcedPlatform("io.netty:netty-bom:4.1.107.Final"), {
    because 'managed version in spring-boot-dependencies (4.1.97.Final) is affected by https://github.com/advisories/GHSA-xpw8-rcwv-8f8p'
  }

  // temporarily overwrite spring-boot dependencies to mitigate CVEs
  // remove once managed by org.springframework.boot:spring-boot-dependencies
  api enforcedPlatform("org.springframework:spring-framework-bom:6.1.5"), {
    because 'CVE-2024-22259'
  }

  // asyncapi generation
  api enforcedPlatform("com.github.victools:jsonschema-generator-bom:${victoolsVersion}")

  constraints {
    // overall testing dependencies
    api 'org.awaitility:awaitility:4.2.1'

    // temporarily overwrite spring-boot dependencies to mitigate CVEs
    // remove once managed by org.springframework.boot:spring-boot-dependencies
    api 'org.springframework.security:spring-security-core:6.2.3', {
      because 'CVE-2024-22257'
    }

    // spring cloud
    api 'org.apache.httpcomponents:httpclient:4.5.14', {
      because 'conflict spring cloud (4.5.4 via maven resolver) and spring-cloud-aws-s3 (4.5.13 via awssdk)'
    }

    // sda-commons-starter-web
    api "org.bouncycastle:bcpkix-jdk18on:${bouncycastleVersion}", {
      because 'fix CVE-2023-33201'
    }
    api "org.bouncycastle:bcprov-jdk18on:${bouncycastleVersion}", {
      because 'fix CVE-2023-33201'
    }
    api "ch.qos.logback.contrib:logback-json-classic:${logbackContribVersion}"
    api "ch.qos.logback.contrib:logback-jackson:${logbackContribVersion}"
    api 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.4.0'

    api 'com.squareup.okio:okio:3.9.0', {
      because "< 3.4.0 CVE-2023-3635"
    }

    // should align with transitive dependency of org.springdoc:springdoc-openapi-webmvc-core:
    api "io.swagger.core.v3:swagger-core-jakarta:${swaggerCoreVersion}"
    api "io.swagger.core.v3:swagger-models-jakarta:${swaggerCoreVersion}"
    api "io.swagger.core.v3:swagger-annotations-jakarta:${swaggerCoreVersion}"

    api 'io.github.classgraph:classgraph:4.8.168'
    api 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    api 'org.yaml:snakeyaml:2.0', {
      because 'Temporary: this version is used to solve CVE-2022-1471'
    }
    api 'org.xerial.snappy:snappy-java:1.1.10.5', {
      because 'fix CVE-2023-34455, CVE-2023-43642'
    }

    // sda-commons-web-testing
    api 'com.nimbusds:nimbus-jose-jwt:9.37.3'

    // match (major) Wiremock version from spring-cloud-contract
    // see https://github.com/spring-cloud/spring-cloud-contract/blob/v4.0.4/spring-cloud-contract-dependencies/pom.xml#L18
    api 'com.github.tomakehurst:wiremock-jre8-standalone:2.35.2'
    api 'net.minidev:json-smart:2.5.1', {
      because 'Temporary: this version is used to solve CVE-2023-1370'
    }
    api 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring3x:4.12.2'

    // sda-commons-starter-kafka
    api "org.scala-lang:scala-reflect:${scalaVersion}", {
      because 'conflict between org.apache.kafka:kafka_2.13:3.1.1 and com.typesafe.scala-logging:scala-logging_2.13:3.9.3'
    }
    api "org.scala-lang:scala-library:${scalaVersion}", {
      because 'conflict between com.fasterxml.jackson.module:jackson-module-scala_2.13:2.13.3, com.typesafe.scala-logging:scala-logging_2.13:3.9.3 and org.scala-lang.modules:scala-collection-compat_2.13:2.4.4'
    }
    api "org.apache.zookeeper:zookeeper:3.9.2", {
      because 'version conflict in transitive managed depedencies'
    }

    // sda-commons-starter-s3
    api "io.awspring.cloud:spring-cloud-aws-s3:3.1.1"

  }
}
